/* XPECT_SETUP eu.numberfour.n4js.spec.tests.N4JSSpecTest END_SETUP */

class A {}
class B extends A {}

class NT extends Object { // must extend Object to be able to test definition site structural types below
	public m() : void {}
}

var nt : NT;


//
// use-site structural types
//

class UST1 {
	public m() : A {return null}
}
class UST2 {
	public m() : A? {}
}

// XPECT errors --> "NT is not a structural subtype of ~UST1: m failed: {function():void} is not a subtype of {function():A}." at "nt"
var t01 : ~UST1 = nt;
// XPECT noerrors -->
var t02 : ~UST2 = nt;



//
// def-site structural types
//

class ~DST1 {
	public m() : A {return null}
}
class ~DST2 {
	public m() : A? {}
}

// XPECT errors --> "NT is not a structural subtype of DST1: m failed: {function():void} is not a subtype of {function():A}." at "nt"
var t11 : DST1 = nt;
// XPECT noerrors -->
var t12 : DST2 = nt;



//
// with-clause
//

var t21 : ~Object with {
	m() : A
};
var t22 : ~Object with {
	m() : A?
};

// XPECT errors --> "NT is not a structural subtype of ~Object with { m(): A }: m failed: {function():void} is not a subtype of {function():A}." at "nt"
t21 = nt;
// XPECT noerrors -->
t22 = nt;

