/*
 * generated by Xtext 2.10.0
 */
package eu.numberfour.n4js.n4mf.formatting2

import com.google.inject.Inject
import eu.numberfour.n4js.n4mf.ModuleFilterSpecifier
import eu.numberfour.n4js.n4mf.ProjectDescription
import eu.numberfour.n4js.n4mf.SimpleProjectDescription
import eu.numberfour.n4js.n4mf.services.N4MFGrammarAccess
import org.eclipse.emf.ecore.EObject
import org.eclipse.xtext.Assignment
import org.eclipse.xtext.Keyword
import org.eclipse.xtext.formatting2.AbstractFormatter2
import org.eclipse.xtext.formatting2.IFormattableDocument
import org.eclipse.xtext.formatting2.IHiddenRegionFormatter

class N4MFFormatter extends AbstractFormatter2 {
	
	static val (IHiddenRegionFormatter)=>void NO_SPACE = [noSpace]
	static val (IHiddenRegionFormatter)=>void ONE_SPACE = [oneSpace]
	static val (IHiddenRegionFormatter)=>void NO_NEW_LINES = [newLines = 0]
	static val (IHiddenRegionFormatter)=>void NEW_LINE = [newLines = 1]
	static val (IHiddenRegionFormatter)=>void INDENT = [indent]

	@Inject extension N4MFGrammarAccess

	def dispatch void format(ProjectDescription it, extension IFormattableDocument document) {
		(#[ it ] + eAllContents.toIterable).forEach[
			commonFormat(document)
		]
		
		allSemanticRegions.filter[ grammarElement.eContainer instanceof Assignment].forEach[
			switch k : nextSemanticRegion.grammarElement {
				Keyword case k.value.length > 1 && k !== moduleFilterSpecifierAccess.inKeyword_1_0:
					append(NEW_LINE)
			}
		]
	}

	def void commonFormat(EObject it, extension IFormattableDocument document) {
		val region = regionFor
		region.keywordPairs('{', '}').forEach[
			interior(
				key.append(NEW_LINE),
				if (value.nextSemanticRegion !== null)
					value.prepend(NEW_LINE).append(NEW_LINE)
				else
					value.prepend(NEW_LINE).append(NO_SPACE),
				INDENT
			)
		]
		
		region.keywords(".", "-").forEach[ surround(NO_SPACE) ]
		region.keywords(",", ":").filter[ grammarElement !== simpleProjectDescriptionAccess.colonKeyword_0_1 ].forEach[ prepend(NO_SPACE).append(ONE_SPACE) ]
		
		region.keywords(",").filter[ grammarElement !== versionConstraintAccess.commaKeyword_0_2_0_0].forEach[ append(NEW_LINE) ]
		region.keywords("(", "[").forEach[ append(NO_SPACE) ]
		region.keywords(")", "]").forEach[ prepend(NO_SPACE) ]

		switch it {
			SimpleProjectDescription:
				region.keyword(simpleProjectDescriptionAccess.colonKeyword_0_1).surround(NO_SPACE)
			ModuleFilterSpecifier:
				region.keyword(moduleFilterSpecifierAccess.inKeyword_1_0).surround(NO_NEW_LINES)
		}
	}
}
